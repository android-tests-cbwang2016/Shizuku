apply plugin: 'idea'

idea.module {
    excludeDirs += file('out')
    resourceDirs += file('magisk-shizuku-starter')
}

buildscript {
    apply from: 'api/manifest.gradle'

    ext.kotlinVersion = '1.5.10'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.15.1'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
    tasks.withType(Javadoc) {
        failOnError false
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def gitCommitId = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
def baseVersionName = "${api_version_major}.6.1"
ext {
    minSdkVersion = 23
    targetSdkVersion = 30
    buildToolsVersion = "30.0.3"
    ndkVersion = "21.3.6528147"
    versionCode = gitCommitCount
    versionName = "${baseVersionName}.r${gitCommitCount}.${gitCommitId}"
}
